TextWindow.Show()
TextWindow.Title = "WebEXE Test Client"
TextWindow.Clear()

tempdir = Text.Append(LDFile.TempFolder,"\webexe")
packagedir = Text.Append(tempdir,"\package")

File.DeleteDirectory(tempdir)
File.CreateDirectory(tempdir)

TextWindow.WriteLine("WELCOME!")

URLGETTER:

TextWindow.WriteLine("")
TextWindow.WriteLine("Enter a server URL (E.G. http://example.com):")
TextWindow.Write(">>> ")
serverurl = TextWindow.Read()

TextWindow.WriteLine("")

urlok = 0
If (Text.GetSubText(serverurl,1,4) = "http") Then
  If (Text.GetSubText(serverurl,5,3) = "://") Then
    If (Text.GetSubText(serverurl,Text.GetLength(serverurl),Text.GetLength(serverurl)) = "/") Then
      TextWindow.WriteLine("Invalid URL: Ends with a /")
    Else
      TextWindow.WriteLine("URL OK, using HTTP.")
      urlok = 1
    EndIf
  ElseIf (Text.GetSubText(serverurl,5,4) = "s://") Then
    If (Text.GetSubText(serverurl,Text.GetLength(serverurl),Text.GetLength(serverurl)) = "/") Then
      TextWindow.WriteLine("Invalid URL: Ends with a /")
    Else
      TextWindow.WriteLine("URL OK, using HTTPS.")
      urlok = 1
    EndIf
  Else
    TextWindow.WriteLine("Invalid URL: No HTTP/HTTPS specified")
  EndIf
Else
  TextWindow.WriteLine("Invalid URL: No HTTP/HTTPS specified")  
EndIf

If (urlok = 0) Then
  Goto URLGETTER
EndIf

INITIALIZER:

TextWindow.WriteLine("")
TextWindow.Write("Downloading list . . . ")
rawlist = ESLNetworking.DownloadString(Text.Append(serverurl,"/list"))
TextWindow.WriteLine("Done.")

TextWindow.Write("Parsing list . . . ")
kindarawlist = LDText.Split(rawlist,"|")
For i = 1 to Array.GetItemCount(kindarawlist)
  list[i] = LDText.Split(kindarawlist[i],":")
EndFor
TextWindow.WriteLine("Done.")

LAUNCHER:

TextWindow.WriteLine("")
TextWindow.WriteLine("Available apps:")
TextWindow.WriteLine("[AppID] --> [ReadableName]")
TextWindow.WriteLine("")

For i = 1 to Array.GetItemCount(list)
  TextWindow.WriteLine(Text.Append(Text.Append(list[i][1]," ---> "),list[i][2]))
EndFor

TextWindow.WriteLine("")
TextWindow.WriteLine("Enter an AppID:")
TextWindow.Write(">>> ")
app2launch = TextWindow.Read()

app2launchok = 0
For i = 1 to Array.GetItemCount(kindarawlist)
  If (app2launch = list[i][1]) Then
    app2launchok = 1
  EndIf
EndFor

If (app2launchok = 0) Then
  TextWindow.WriteLine("Invalid.")
  Goto LAUNCHER
Else
  Goto LOADER
EndIf

LOADER:

TextWindow.WriteLine("")
TextWindow.Write("Prepping temp dir . . . ")
File.DeleteDirectory(tempdir)
File.CreateDirectory(tempdir)
File.CreateDirectory(packagedir)
TextWindow.WriteLine("Done.")

TextWindow.Write("Downloading app package . . . ")
ESLNetworking.DownloadFile(Text.Append(serverurl,Text.Append("/app/",Text.Append(app2launch,".zip"))),Text.Append(tempdir,"\package.zip"))
TextWindow.WriteLine("Done.")

EXECUTOR:

TextWindow.Write("Extracting app package . . . ")
LDZip.UnZip(Text.Append(tempdir,"\package.zip"),packagedir)
TextWindow.WriteLine("Done.")

TextWindow.Write("Executing . . . ")
SPProcess.Start(Text.Append(packagedir,"\webexe.boot.exe"))
TextWindow.WriteLine("Process started.")

TextWindow.Write("PRESS ANY KEY WHEN THE APPLICATION CLOSES . . . ")
TextWindow.PauseWithoutMessage()
TextWindow.WriteLine("")

Goto INITIALIZER